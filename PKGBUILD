# Maintainer: Si Feng <sci.feng at gmail.com>
# Contributor: Andreas Radke <andyrtr at archlinux.org>
 
pkgbase=kernel26-lts-xen
pkgname=('kernel26-lts-xen') # Build stock -lts kernel with Xen guest support
_kernelname=${pkgname#kernel26}
_basekernel=2.6.32
pkgver=${_basekernel}.57
pkgrel=1
arch=('i686')
license=('GPL2')
url="http://www.kernel.org"
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
        ftp://ftp.kernel.org/pub/linux/kernel/v2.6/longterm/v$_basekernel/patch-$pkgver.bz2
        # the main kernel config files
        config
        # standard config files for mkinitcpio ramdisk
        ${pkgname}.preset)
options=(!strip)
md5sums=('260551284ac224c3a43c4adac7df4879'
         '3d469b007843c62360701d6d973e56e3'
         '050a8944aa461b6fa3c869c07aceb286'
         'b042f1a22bd8775883c468b02332ae00')

build() {
  cd ${srcdir}/linux-$_basekernel
  if [ "$_basekernel" != "$pkgver" ]; then
    # add latest kernel stable patch
    patch -Np1 -i ${srcdir}/patch-$pkgver
  fi
  
  cat ../config >./.config
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
  fi
  # set extraversion to pkgrel
  sed -ri "s|^(EXTRAVERSION =).*|\1 ${pkgver#$_basekernel}-${pkgrel}|" Makefile


  # get kernel version
  make prepare
  # load configuration
  # Configure the kernel. Replace the line below with one of your choice.
  #make menuconfig # CLI menu for configuration
  #make xconfig # X-based configuration
  #make oldconfig # using old config from previous kernel version
  # ... or manually edit .config
  ####################
  # stop here
  # this is useful to configure the kernel
  #msg "Stopping build"
  # return 1
  ####################
  yes "" | make config
  # build!
  make ${MAKEFLAGS} bzImage modules
}

package_kernel26-lts-xen() {
  pkgdesc="The Linux Kernel and modules with Xen guest support - stable longtime supported kernel package suitable for servers"
  backup=(etc/mkinitcpio.d/${pkgname}.preset)
  depends=('coreutils' 'kernel26-firmware>=2.6.32' 'module-init-tools>=3.12-2' 'mkinitcpio>=0.6.8-2')
  provides=('kernel26-lts')
  install=${pkgname}.install

  KARCH=x86
  cd ${srcdir}/linux-$_basekernel
  # get kernel version
  _kernver="$(make kernelrelease)"
  mkdir -p ${pkgdir}/{lib/modules,lib/firmware,boot}
  make INSTALL_MOD_PATH=${pkgdir} modules_install
  cp System.map ${pkgdir}/boot/System.map26${_kernelname}
  cp arch/$KARCH/boot/bzImage ${pkgdir}/boot/vmlinuz26${_kernelname}
  # add vmlinux
  install -m644 -D vmlinux ${pkgdir}/usr/src/linux-${_kernver}/vmlinux

  # install fallback mkinitcpio.conf file and preset file for kernel
  install -m644 -D ${srcdir}/${pkgname}.preset ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset
  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i $startdir/${pkgname}.install
  sed \
    -e "s|source .*|source /etc/mkinitcpio.d/kernel26${_kernelname}.kver|g" \
    -e "s|default_image=.*|default_image=\"/boot/${pkgname}.img\"|g" \
    -e "s|fallback_image=.*|fallback_image=\"/boot/${pkgname}-fallback.img\"|g" \
    -i ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset

  echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > ${pkgdir}/etc/mkinitcpio.d/${pkgname}.kver
  # remove build and source links
  rm -f ${pkgdir}/lib/modules/${_kernver}/{source,build}
  # remove the firmware
  rm -rf ${pkgdir}/lib/firmware
  # gzip -9 all modules to save 100MB of space
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
  # make room for external modules
  ln -s "../extramodules-${_basekernel}${_kernelname:--ARCH}" "${pkgdir}/lib/modules/${_kernver}/extramodules"
  # add real version for building the kernel
  mkdir -p "${pkgdir}/lib/modules/extramodules-${_basekernel}${_kernelname:--ARCH}"
  echo "${_kernver}" > "${pkgdir}/lib/modules/extramodules-${_basekernel}${_kernelname:--ARCH}/version"
}
